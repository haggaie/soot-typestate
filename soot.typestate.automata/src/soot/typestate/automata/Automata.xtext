grammar soot.typestate.automata.Automata with org.eclipse.xtext.common.Terminals

generate automata "http://code.google.com/p/soot-typestate/"

Automata :
	(package=Package)?
	(automata+=Automaton)+ ;

Package : 'package' name=JAVAID ';' ;

Automaton :
	'automaton' klass=Class '{'
		'initial' '=' initialState=[State] ';'
		('error' '=' errorState=[State] ';')?
		(states+=State)+
	'}';

Class : name=JAVAID ;

State :
	'state' name=ID '{'
		(transitions+=Transition)*
	'}';

Transition: InvocationTransition | BranchedTransition;

InvocationTransition :
	(invocation=Invocation) '->' (state=[State]) ';';
	
BranchedTransition:
	(invocation=Invocation) 'returns' (value=BooleanLiteral) '->'
	(state=[State]) ';' ;

Invocation: Method | Constructor ;

Method :
	(returntype=Type) (name=ID) '(' (args+=Type (',' args+=Type)*)? ')';

Constructor :
	klass=[Class] '(' (args+=Type (',' args+=Type)*)? ')' ;

Type : name=JAVAID ;

JAVAID : ID('.' ID)*;

BooleanLiteral: (true?='true') | 'false' ;